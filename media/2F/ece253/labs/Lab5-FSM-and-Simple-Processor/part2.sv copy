`timescale 1ns /1 ns
/************************** Control path **************************************************/
module control_path(
    input logic clk,
    input logic reset, 
    input logic run, 
    input logic [15:0] INSTRin,
    output logic R0in, R1in, Ain, Rin, IRin, 
    output logic [1:0] select, ALUOP,
    output logic done
); 

/* OPCODE format: II M X DDDDDDDDDDDD, where 
    *     II = instruction, M = Immediate, X = rX; X = (rX==0) ? r0:r1
    *     If M = 0, DDDDDDDDDDDD = 00000000000Y = rY; Y = (rY==0)? r0:r1
    *     If M = 1, DDDDDDDDDDDD = #D is the immediate operand 
    *
    *  II M  Instruction   Description
    *  -- -  -----------   -----------
    *  00 0: mv    rX,rY    rX <- rY
    *  00 1: mv    rX,#D    rX <- D (sign extended)
    *  01 0: add   rX,rY    rX <- rX + rY
    *  01 1: add   rX,#D    rX <- rX + D
    *  10 0: sub   rX,rY    rX <- rX - rY
    *  10 1: sub   rX,#D    rX <- rX - D
    *  11 0: mult  rX,rY    rX <- rX * rY
    *  11 1: mult  rX,#D    rX <- rX * D 
*/

parameter mv = 2'b00, add = 2'b01, sub = 2'b10, mult = 2'b11;

logic [1:0] II;
logic M, rX, rY;

assign II = INSTRin[15:14];
assign M =  INSTRin[13];
assign rX = INSTRin[12];
assign rY = INSTRin[0];

// control FSM states
typedef enum logic[1:0]
{
    C0 = 2'd0,
    C1 = 2'd1, 
    C2 = 2'd2, 
    C3 = 2'd3
} statetype;

statetype current_state, next_state;


// control FSM state table
always_comb begin
    case(current_state)
	    C0: next_state = run? C1:C0;
        C1: begin
            if(II==mv)
                next_state=C0;
            else
                next_state=C2;
        end
        C2: next_state = C3;
        C3: next_state = C0;
        default: next_state=C0;
    endcase
end

// output logic i.e: datapath control signals
always_comb begin
    // by default, make all our signals 0
    R0in = 1'b0; R1in = 1'b0;
    Ain = 1'b0; Rin = 1'b0; IRin = 1'b0;
    select = 2'b00; 
    ALUOP = 2'b00;
    done = 1'b0;

    case(current_state)
        C0: IRin=1'b1;
        C1: begin case(II)
                mv: begin
                    if(M ==1'b0) begin //register mode
                        if(rY==0) select=2'b01;
                        else select=2'b10;
                    end else begin // immediate mode
                        select = 2'b11; // select IR
                    end 
                    if(rX==1'b0) R0in = 1'b1; else R1in=1'b1; 
                    done = 1'b1;
                end
                add, sub, mult: begin
                    if(rX==1'b0) select = 2'b01; //r0
                    else select = 2'b10; // r1
                    Ain=1'b1;
                end
            endcase
        end 
        C2: begin case(II)
                add, mult, sub: begin
                    Rin=1'b1; 
                    if(M ==1'b0) begin //register mode
                        if(rY==0) select=2'b01;
                        else select=2'b10;
                    end else begin // immediate mode
                        select = 2'b11; // select IR
                    end 
                    case(II)
                        add: ALUOP=2'b00;
                        sub: ALUOP=2'b01;
                        mult: ALUOP=2'b10;
                        default: ALUOP=2'b11;
                    endcase
                end
                default: done=1'b1;
            endcase
        end
        C3: begin case(II)
                add, mult, sub: begin
                    select=2'b00; 
                    if(rX==1'b0) R0in=1'b1; else R1in=1'b1;
                    done = 1'b1;
                end
                default: done=1'b1;
            endcase
            end
    endcase 
end


// control FSM FlipFlop
always_ff @(posedge clk) begin
    if(reset)
        current_state <= C0;
    else
       current_state <= next_state;
end

endmodule




/************************** Datapath **************************************************/
module datapath(
    input logic clk, 
    input logic reset,
    input logic [15:0] INSTRin,
    input logic IRin, R0in, R1in, Ain, Rin,
    input logic [1:0] select, ALUOP,
    output logic [15:0] r0, r1, a, r // for testing purposes these are outputs
);
logic [15:0] IR;
logic [15:0] mux_out;
logic [15:0] alu_out;
logic [15:0] sign_extended_imm;
logic [15:0] r0_reg, r1_reg, a_reg, r_reg;

//sign extend for immediates
assign sign_extended_imm={{4{IR[11]}},IR[11:0]}; // copy the most sig bits

//mux to select input for ALU and data path
always_comb begin 
    case(select)
        2'b00: mux_out= r_reg;
        2'b01: mux_out=r0_reg;
        2'b10: mux_out=r1_reg;
        2'b11: mux_out= sign_extended_imm;
        default: mux_out=16'b0;
    endcase
end

//alu doing things
always_comb begin
    case(ALUOP)
        2'b00: alu_out=a_reg+mux_out;
        2'b01: alu_out=a_reg-mux_out;
        2'b10: alu_out=a_reg*mux_out;
        default: alu_out = 16'b0;
    endcase
end 

//instruction reg
always_ff@(posedge clk) begin
    if(reset) 
        IR<=16'b0;
    else if(IRin) // acts as enable
        IR<=INSTRin;
end

//r0 reg
always_ff@(posedge clk) begin
    if(reset) 
        r0_reg<=16'b0;
    else if(R0in) // acts as enable
        r0_reg<=mux_out;
end

//r1 reg
always_ff@(posedge clk) begin
    if(reset) 
        r1_reg<=16'b0;
    else if(R1in) // acts as enable
        r1_reg<=mux_out;
end

//a reg
always_ff@(posedge clk) begin
    if(reset) 
        a_reg<=16'b0;
    else if(Ain) // acts as enable
        a_reg<=mux_out;
end

// r reg
always_ff@(posedge clk) begin
    if(reset)
        r_reg<=16'b0;
    else if(Rin) // acts as enable
        r_reg<=alu_out;
end
assign r0 = r0_reg;
assign r1 = r1_reg;
assign a = a_reg;
assign r = r_reg;
endmodule



/************************** processor  **************************************************/
module part2(
    input logic [15:0] INSTRin,
    input logic reset, 
    input logic clk,
    input logic run,
    output logic done,
    output logic[15:0] r0_out,r1_out, a_out, r_out
);

// intermediate logic 
logic r0in, r1in, ain, rin, irin;
logic[1:0] select, aluop;

control_path control(
   .clk(clk),
   .reset(reset), 
   .run(run), 
   .INSTRin(INSTRin),
   .R0in(r0in), 
   .R1in(r1in), 
   .Ain(ain), 
   .Rin(rin), 
   .IRin(irin), 
   .select(select), 
   .ALUOP(aluop),
   .done(done)
);

datapath data(
    .clk(clk), 
    .reset(reset),
    .INSTRin(INSTRin),
    .IRin(irin), 
    .R0in(r0in),
    .R1in(r1in), 
    .Ain(ain),
    .Rin(rin),
    .select(select), 
    .ALUOP(aluop),
    .r0(r0_out), 
    .r1(r1_out),
    .a(a_out),
    .r(r_out)
);

endmodule
